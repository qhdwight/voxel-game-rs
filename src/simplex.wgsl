struct PointBuffer {
    data: array<vec2<f32>>;
};

struct HeightBuffer {
    data: array<f32>;
};

[[group(0), binding(0)]]
var<storage, read> in_points: PointBuffer;

[[group(0), binding(1)]]
var<storage, read_write> out_heights: HeightBuffer;

fn permute3(x: vec3<f32>) -> vec3<f32> {
    return (((x * 34.) + 1.) * x) % vec3<f32>(289.);
}

fn simplexNoise2(v: vec2<f32>) -> f32 {
  let C = vec4<f32>(0.211324865405187, 0.366025403784439, -0.577350269189626, 0.024390243902439);
  var i: vec2<f32> = floor(v + dot(v, C.yy));
  let x0 = v - i + dot(i, C.xx);
  var i1: vec2<f32> = select(vec2<f32>(1., 0.), vec2<f32>(0., 1.), (x0.x > x0.y));
  var x12: vec4<f32> = x0.xyxy + C.xxzz - vec4<f32>(i1, 0., 0.);
  i = i % vec2<f32>(289.);
  let p = permute3(permute3(i.y + vec3<f32>(0., i1.y, 1.)) + i.x + vec3<f32>(0., i1.x, 1.));
  var m: vec3<f32> = max(0.5 - vec3<f32>(dot(x0, x0), dot(x12.xy, x12.xy), dot(x12.zw, x12.zw)), vec3<f32>(0.));
  m = m * m;
  m = m * m;
  let x = 2. * fract(p * C.www) - 1.;
  let h = abs(x) - 0.5;
  let ox = floor(x + 0.5);
  let a0 = x - ox;
  m = m * (1.79284291400159 - 0.85373472095314 * (a0 * a0 + h * h));
  let g = vec3<f32>(a0.x * x0.x + h.x * x0.y, a0.yz * x12.xz + h.yz * x12.yw);
  return 130. * dot(m, g);
}

[[stage(compute), workgroup_size(32, 1, 1)]]
fn main([[builtin(global_invocation_id)]] invocation_id: vec3<u32>) {
    let point = in_points.data[invocation_id.x];
    out_heights.data[invocation_id.x] = simplexNoise2(point);
}
